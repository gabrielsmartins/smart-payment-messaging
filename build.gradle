apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = '11'
targetCompatibility = '11'
compileJava.options.encoding = 'UTF-8'

apply from: "$rootDir/repositories.gradle", to: buildscript

buildscript {
    apply from: "$rootDir/repositories.gradle", to: buildscript
    ext {
        springBootPluginVersion = '2.4.3'
        springBootDependencyManagementPluginVersion = '1.0.11.RELEASE'
        avroPluginVersion = '0.22.0'
    }
    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: springBootPluginVersion
        classpath group: 'io.spring.gradle', name: 'dependency-management-plugin', version: springBootDependencyManagementPluginVersion
        classpath group: 'com.commercehub.gradle.plugin', name: 'gradle-avro-plugin', version: avroPluginVersion
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = '11'
    targetCompatibility = '11'

    compileJava.options.encoding = 'UTF-8'
    apply from: "$rootDir/repositories.gradle"
}

subprojects {
    apply plugin: 'io.spring.dependency-management'

    group = 'br.gabrielsmartins.smartpayment'
    version = '0.0.1-SNAPSHOT'

    ext {
        springBootVersion = '2.4.2'
        springCloudVersion = '2020.0.1'
        lombokVersion = '1.18.12'
        junitVersion = '5.6.2'
        junitPlatformVersion = '1.6.2'
        assertjVersion = '3.16.1'
        apacheCommons3Version = '3.9'
        mapStructVersion = '1.3.1.Final'
        mockitoVersion = '3.3.3'
        avroVersion = '1.10.1'
        confluentVersion = '6.0.1'
        awaitilityVersion = '4.0.3'
        logstashVersion = '6.4'
    }

    dependencies {

        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
        testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformVersion
        testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: junitPlatformVersion
        testImplementation group: 'org.assertj', name: 'assertj-core', version: assertjVersion
        testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion

        implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

        implementation group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommons3Version

        implementation group: 'org.mapstruct', name: 'mapstruct', version: mapStructVersion
        annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: mapStructVersion

        implementation 'org.springframework.boot:spring-boot-starter-webflux'

        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
        implementation 'org.springframework.cloud:spring-cloud-sleuth-zipkin'

        implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: logstashVersion

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.projectreactor:reactor-test'
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
        }
    }

    test {
        useJUnitPlatform()
        finalizedBy(jacocoTestReport)
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
            html.destination file("${buildDir}/jacocoHtml")
        }
    }
}

jacoco {
    toolVersion = '0.8.5'
}

